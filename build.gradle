plugins {
	id "com.google.protobuf" version "0.8.5"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "maven-publish"
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'

group = 'com.github.BAData'
version = externalVersion

repositories {
    mavenCentral()
}

[ compileJava, compileTestJava ]*.options*.encoding = 'UTF-8'

sourceCompatibility='1.7'
targetCompatibility='1.7'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    generatedFilesBaseDir = "$projectDir/src"
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.5.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'GNU General Public License v3.0'
                    url 'http://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}

publishing {
  publications {
    maven(MavenPublication) {
		groupId group
		artifact jar
        artifact sourcesJar
        artifact javadocJar
    }
  }
  
  repositories {
    maven {
      url nexusUrl
      name "nexus"
      credentials {
          username nexusUserName
          password nexusPassword
      }
    }
  }
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}